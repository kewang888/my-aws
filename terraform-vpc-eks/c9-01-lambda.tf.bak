resource "aws_security_group" "lambda_eks_tester_sg" {
  vpc_id = module.vpc.vpc_id

  # Allow Lambda to make outbound requests (e.g., to a database)
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

# IAM Role for Lambda Execution
resource "aws_iam_role" "lambda_eks_tester_role" {
  name = "lambda_eks_tester_execution_role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "lambda.amazonaws.com"
        }
      }
    ]
  })
}

resource "aws_iam_policy_attachment" "lambda_vpc_execution" {
  name       = "lambda_vpc_execution"
  roles      = [aws_iam_role.lambda_eks_tester_role.name]
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaVPCAccessExecutionRole"
}

resource "aws_iam_policy" "eks_describe_policy" {
  name        = "EKSDescribePolicy"
  description = "Allows describing the EKS cluster"

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect   = "Allow"
        Action   = "eks:DescribeCluster"
        Resource = "arn:aws:eks:${var.aws_region}:${data.aws_caller_identity.current.account_id}:cluster/${aws_eks_cluster.eks_cluster.name}"
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "eks_policy_attachment" {
  role       = aws_iam_role.lambda_eks_tester_role.name
  policy_arn = aws_iam_policy.eks_describe_policy.arn
}

# Lambda Function inside VPC
resource "aws_lambda_function" "lambda_eks_tester" {
  function_name    = "lambda_eks_tester"
  role             = aws_iam_role.lambda_eks_tester_role.arn
  handler          = "lambda_function.lambda_handler"
  runtime          = "python3.12"
  filename         = "lambda.zip"
  source_code_hash = filebase64sha256("lambda.zip")

  # Attach Lambda to VPC
  vpc_config {
    subnet_ids         = module.vpc.private_subnets
    security_group_ids = [aws_security_group.lambda_eks_tester_sg.id]
  }

  environment {
    variables = {
      ENV_VAR = "value"
    }
  }
}

resource "aws_security_group_rule" "cluster_control_plane_sg_ingress_for_lambda_eks_tester" {
  description              = "Allow lambda_eks_tester to communicate with the cluster API Server"
  type                     = "ingress"
  from_port                = 443
  to_port                  = 443
  protocol                 = "tcp"
  security_group_id        = aws_eks_cluster.eks_cluster.vpc_config[0].cluster_security_group_id
  source_security_group_id = aws_security_group.lambda_eks_tester_sg.id
}

resource "awscc_eks_access_entry" "lambda_eks_tester_role_entry" {
  cluster_name  = aws_eks_cluster.eks_cluster.name
  principal_arn = aws_iam_role.lambda_eks_tester_role.arn
  type          = "STANDARD"
  username      = "${aws_iam_role.lambda_eks_tester_role.arn}/{{SessionName}}"
  # kubernetes_groups = ["my-endpoint-reader"]

  access_policies = [
    {
      access_scope = {
        type = "cluster"
      },
      policy_arn = "arn:aws:eks::aws:cluster-access-policy/AmazonEKSClusterAdminPolicy"
    }
  ]

  tags = [{
    key   = "Modified By"
    value = "AWSCC"
  }]
}
